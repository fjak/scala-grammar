language Scala
start symbol NoLExpr

/* Literal */
test literal NoLExpression [[
	42
]] parse to LiteralNoLExpr(_)

test long NoLExpression [[
	42L
]] parse to LiteralNoLExpr(_)

test id NoLExpression [[
	foo
]]

/* Path */
test simple path [[
	some.foo
]]

test path [[
	scala.collection.mutable.Map
]] parse to PathNoLExpr(_)


/* Wildcard */
test wildcard NoLExpression [[
	_
]] parse to WildcardNoLExpr


/* Tuple */
test braced NoLExpression [[
	(23)
]] parse to TupleNoLExpr([LiteralNoLExpr(Int("23"))])

test tuple NoLExpression [[
	(23, 42)
]] parse to TupleNoLExpr(_)

test recursive brace NoLExpression [[
	(((42)))
]] parse to TupleNoLExpr([TupleNoLExpr([TupleNoLExpr(_)])])


/* Designator */
test designator [[
	f(23).foo.bar
]] parse to DesignatorNoLExpr(_, _)

test designator NoLExpression allows blocks [[
	{Person("John", "Doe")}.first_name
]]

test designator of new class instantiation [[
	new Person().default_name
]] parse to DesignatorNoLExpr(_, _)

/* Type Application */
test type application [[
	42 [Float]
]] parse to TypeApplication(_, _)


/* Application */
test application [[
	f(42)
]] parse to AppNoLExpr(_, _)

test multiple arg lists [[
	f(23)(42)
]] parse to AppNoLExpr(AppNoLExpr(_, _), _)

test application spread layout [[
	f
	(42)
]] parse to AppNoLExpr(_, _)

test application valid layout [[
	f (
		42
	)
]] parse to AppNoLExpr(_, _)

test application wildcard target [[
	_(23)
]] parse to AppNoLExpr(WildcardNoLExpr, _)

test application literal target [[
	42(23)
]] parse to AppNoLExpr(LiteralNoLExpr(_), _)

test application path target [[
	scala.collection.mutable.Map()
]] parse to AppNoLExpr(PathNoLExpr(_), _)

test application block target is invalid [[
	{foo}(23)
]] parse fails

test application block application target [[
	Map {'x -> 23} ('x)
]] parse to AppNoLExpr(BlockAppNoLExpr(_, _), _)

test multiple arg lists spread layout [[
	f(23)
	(42)
]] parse to AppNoLExpr(_, _)

test multiple arg lists valid layout [[
	f  (
		23)  (
			42)
]] parse to AppNoLExpr(AppNoLExpr(_, _), _)

test block argument [[
	f {23}
]] parse to BlockAppNoLExpr(_, _)

test block argument valid layout [[
	f
	{23}
]] parse to BlockAppNoLExpr(_, _)

test block argument spread layout [[
	f
	
	{23}
]] parse to BlockAppNoLExpr(_, _)


/* Instantiation */
test simple class instantiation [[
	new Foo()
]] parse to NewClassNoLExpr(_)

test argument less class instantiation [[
	new Foo
]] parse to NewClassNoLExpr(_)

test multi argument list class instantiation [[
	new Foo(42)(23)
]] parse to NewClassNoLExpr(_)

test class instantiation layout [[
	new
	
	Foo(42)
]] parse to NewClassNoLExpr(_)

test template instantiation [[
	new { def f() = println("42")}
]] parse to NewTemplateNoLExpr(_)

test empty template instantiation [[
	new {}
]] parse to NewTemplateNoLExpr(_)

test template instantiation layout [[
	new
	
	{}
]] parse to NewTemplateNoLExpr(_)

test new fqn class instantiation [[
	new scala.collection.mutable.Map
]] parse to NewClassNoLExpr(_)


/* Blocks */
test block [[
	{ println("Hello") }
]] parse to BlockExpr(_)

test block layout [[
	{
		
		println("Hello")
		
		println("You")
		
	}
]] parse to BlockExpr(_)

/* Eta Expansion */
test eta expansion [[
	f _
]] parse to EtaExpansionNoLExpr(_)

test eta expansion spread layout [[
	f
	_
]] parse to EtaExpansionNoLExpr(_)

test block eta expansion [[
	{ def f(n: Int) = println(n) ; f } _
]] parse to EtaExpansionNoLExpr(_)

test DesignatorExpr on Int [[
	10000.toFloat
]] parse to DesignatorNoLExpr(LiteralNoLExpr(_), _)

test DesignatorExpr on Float [[
	10000.000.toInt
]] parse to DesignatorNoLExpr(LiteralNoLExpr(_), _)
